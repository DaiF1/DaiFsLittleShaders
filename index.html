<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8" />
        <link rel="icon" type="image/svg+xml" href="/DaiFsLittleShaders/vite.svg" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <title>DaiF's Little Shaders</title>
      <script type="module" crossorigin src="/DaiFsLittleShaders/assets/index-a509606e.js"></script>
      <link rel="stylesheet" href="/DaiFsLittleShaders/assets/index-63b7d29d.css">
    </head>
    <body>
        <select id="shader-select">
            <option value="default" selected>Default</option>
            <option value="gooch">Gooch</option>
            <option value="comics">Comics</option>
            <option value="drawing">Hand-Drawn</option>
        </select>
        <canvas id="c"></canvas>
        
        <script id="default-vertex" type="shader/vertex">
            attribute vec4 a_position;
            attribute vec2 a_texcoord;
            attribute vec3 a_normal;

            uniform mat4 u_worldViewMatrix;
            uniform mat4 u_world;

            varying vec4 v_position;
            varying vec2 v_texcoord;
            varying vec3 v_normal;

            void main() {
                gl_Position = u_worldViewMatrix * a_position;

                v_position = u_world * a_position;
                v_texcoord = a_texcoord;
                v_normal = mat3(u_world) * a_normal;
            }
        </script>

        <script id="default-fragment" type="shader/fragment">
            precision mediump float;

            varying vec2 v_texcoord;
            varying vec3 v_normal;

            uniform sampler2D u_texture;
            uniform vec3 u_reverseLightDir;

            void main() {
                vec3 normal = normalize(v_normal);
                float light = (1.0 + dot(normal, u_reverseLightDir)) / 2.0;

                gl_FragColor = texture2D(u_texture, v_texcoord);
                gl_FragColor.rgb *= light;
            }
        </script>

        <script id="vertex-depth" type="shader/vertex">
            attribute vec4 a_position;
            attribute vec3 a_normal;

            uniform mat4 u_worldViewMatrix;
            uniform mat4 u_world;

            varying float v_depth;
            varying vec3 v_normal;

            void main() {
                gl_Position = u_worldViewMatrix * a_position;

                v_depth = gl_Position.z;
                v_normal = mat3(u_world) * a_normal;
            }
        </script>

        <script id="fragment-depth" type="shader/fragment">
            precision mediump float;

            varying float v_depth;
            varying vec3 v_normal;

            void main() {
                vec3 color = vec3(0, 0, 0);
                gl_FragColor = vec4(color, 1);
            }
        </script>

        <script id="gooch-fragment" type="shader/fragment">
            precision mediump float;

            varying vec2 v_texcoord;
            varying vec3 v_normal;

            uniform sampler2D u_texture;
            uniform vec3 u_reverseLightDir;

            void main() {
                vec3 normal = normalize(v_normal);
                float light = (1.0 + dot(normal, u_reverseLightDir)) / 2.0;
                vec4 color = texture2D(u_texture, v_texcoord);

                float alpha = 0.2;
                float beta = 0.4;
                vec3 coolColor = vec3(129.0 / 255.0, 188.0 / 255.0, 230.0 / 255.0);
                vec3 warmColor = vec3(230.0 / 255.0, 189.0 / 255.0, 129.0 / 255.0);

                vec3 kCool = coolColor + vec3(alpha) * color.xyz;
                vec3 kWarm = warmColor + vec3(beta) * color.xyz;

                vec3 gooch = (light * kWarm) + ((1.0 - light) * kCool);
                gl_FragColor = vec4(gooch, 1);
            }
        </script>

        <script id="comics-fragment" type="shader/fragment">
            precision mediump float;

            varying vec2 v_texcoord;
            varying vec3 v_normal;

            uniform sampler2D u_texture;
            uniform vec3 u_reverseLightDir;

            void main() {
                // color
                vec3 normal = normalize(v_normal);
                float light = (1.0 + dot(normal, u_reverseLightDir)) / 2.0;
                vec4 color = texture2D(u_texture, v_texcoord);

                vec3 shadow = vec3(0.5, 0.5, 0.8);
                float f = step(0.2, light) * (0.5 * step(0.4, light) + 0.5);
                vec3 col = vec3(f) * color.xyz + vec3(1.0 - f) * color.xyz * shadow.xyz;

                gl_FragColor = vec4(col, 1);
            }
        </script>

        <script id="drawing-fragment" type="shader/fragment">
            precision mediump float;

            varying vec4 v_position;
            varying vec2 v_texcoord;
            varying vec3 v_normal;

            uniform sampler2D u_texture;
            uniform sampler2D u_textureHash;
            uniform vec3 u_reverseLightDir;
            uniform mat4 u_cameraView;
            uniform float u_time;

            float rand(vec2 st) {
                return fract(sin(dot(st.xy, vec2(12.9898,78.233))) * 43758.5453123);
            }

            float noise(vec2 st) {
                vec2 i = floor(st);
                vec2 f = fract(st);

                float a = rand(i);
                float b = rand(i + vec2(1.0, 0.0));
                float c = rand(i + vec2(0.0, 1.0));
                float d = rand(i + vec2(1.0, 1.0));

                vec2 u = smoothstep(0.,1.,f);

                return mix(a, b, u.x) +
                        (c - a)* u.y * (1.0 - u.x) +
                        (d - b) * u.x * u.y;
            }

            void main() {
                // color
                vec3 normal = normalize(v_normal);
                float light = (1.0 + dot(normal, u_reverseLightDir)) / 2.0;

                // generated coords
                float scale = 2.0;
                float time = cos(u_time * 10.0);
                time = (floor(time) + ceil(time)) * 0.25;
                vec4 pos = v_position * u_cameraView;
                vec2 st = vec2(pos.x * scale, pos.y * scale * 0.5 + time);

                vec2 noiseSt = vec2(v_position.x * scale, v_position.y * scale);
                vec3 filter = vec3(step(0.3, noise(noiseSt * 100.0)) * (1.0 - step(0.4, light)) - 
                    step(0.8, noise(noiseSt * 10.0) * 0.5 + noise(noiseSt * 2.0)) * (step(0.388, light)));
                vec3 colorHash = texture2D(u_textureHash, st).rgb * filter;
                
                vec3 shadow = vec3(0.5, 0.5, 0.5);
                float f = step(0.2, light);
                vec3 col = 1.0 - vec3((f - step(0.205, light))) * step(0.3, noise(noiseSt * 10.0)) * shadow.rgb - 
                    vec3(1.0 - f) * shadow.rgb;

                gl_FragColor = vec4(col - colorHash, 1.0);
            }
        </script>

        <script id="default-vertex-postp" type="shader/vertex">
            attribute vec4 a_position;
            attribute vec2 a_texcoord;

            varying vec2 v_texcoord;

            void main() {
                gl_Position = a_position;
                v_texcoord = a_texcoord;
            }
        </script>

        <script id="default-fragment-postp" type="shader/fragment">
            precision mediump float;

            varying vec2 v_texcoord;

            uniform sampler2D u_screenTexture;

            void main() {
                gl_FragColor = texture2D(u_screenTexture, v_texcoord);
            }
        </script>

        <script id="outline-fragment-postp" type="shader/fragment">
            precision mediump float;

            varying vec2 v_texcoord;

            uniform sampler2D u_depthTexture;
            uniform sampler2D u_screenTexture;
            uniform float u_width;
            uniform float u_height;

            void main() {
                const float outlineWidth = 5.0;

                vec3 n[9];
                float w = (1.0 / u_width) * outlineWidth;
                float h = (1.0 / u_height) * outlineWidth;

                vec3 leftColor = texture2D(u_depthTexture, v_texcoord + vec2(-w, 0)).rgb;
                vec3 rightColor = texture2D(u_depthTexture, v_texcoord + vec2(w, 0)).rgb;
                vec3 topColor = texture2D(u_depthTexture, v_texcoord + vec2(0, h)).rgb;
                vec3 bottomColor = texture2D(u_depthTexture, v_texcoord + vec2(0, -h)).rgb;

                vec2 gradient = vec2(length(rightColor - leftColor), length(topColor - bottomColor));
                float outline = 1.0 - step(0.2, length(gradient));

                vec3 color = vec3(outline) * texture2D(u_screenTexture, v_texcoord).rgb;
                gl_FragColor = vec4(color, 1.0);
            }
        </script>
    </body>
</html>
